# 1. Setup & Configuration
##### load config and sample annotation sheets FIRST #####
configfile: os.path.join("config", "config.yml")

# Now include common.smk which uses the config
include: "rules/common.smk"

# 2. Domain Logic (in dependency order)
include: "rules/qc.smk"          # Fastp, MultiQC
include: "rules/align.smk"       # BWA, Bowtie2, Chromap
include: "rules/pairtools.smk"    # Pairtools
include: "rules/hicpro_style.smk" # HiC-Pro style processing
include: "rules/cooltools.smk"    # Cooler generation
include: "rules/downstream.smk"   # Downstream analysis

# 3. Output Definitions
# Note: min_resolution is defined in common.smk

# Setting up scaling from pairs outputs if required
library_scaling_pairs = []
if "scaling_pairs" in config and config["scaling_pairs"].get("do", True):
    library_scaling_pairs = expand(
        f"{outdir}/Report/Pairs/{{library}}.nodups.scaling.tsv",
        library=SAMPLE_FASTQS.keys(),
    )
    if not config["scaling_pairs"].get("max_distance", False):
        chromsizes = pd.read_table(
            chromsizes_path, header=None, names=["chrom", "size"]
        )
        config["scaling_pairs"]["max_distance"] = chromsizes["size"].max()

# Setting up the library cooler outputs
library_coolers = expand(
    f"{outdir}/Important_processed/Cooler/{{library}}.{{filter_name}}.{min_resolution}.mcool",
    library=SAMPLE_FASTQS.keys(),
    filter_name=list(config["bin"]["filters"].keys()),
)

library_downstream_stat = expand(
    f"{outdir}/downstream_res/downstream_dist/{{library}}.{{filter_name}}.{min_resolution}_loglog_fits.csv",
    library=SAMPLE_FASTQS.keys(),
    filter_name=list(config["bin"]["filters"].keys()),
)

library_mustache_loops = expand(
    f"{outdir}/Important_processed/Loops/{{library}}.{{filter_name}}.{{resolution}}_loops.tsv",
    library=SAMPLE_FASTQS.keys(),
    filter_name=list(config["bin"]["filters"].keys()),
    resolution=config["bin"]["resolution_for_mustache"],
)

# Collect all report files
library_report = []
# Alignment statistics
for sample in SAMPLE_FASTQS.keys():
    if config["map"]["mapper"] == "bowtie2":
        library_report.append(f"{outdir}/Report/align/{sample}.pairstat")
        library_report.append(f"{outdir}/Report/align/{sample}_R1.mapstat")
        library_report.append(f"{outdir}/Report/align/{sample}_R2.mapstat")
# Pairs statistics (dedup stats linked to Report/pairtools)
library_report += expand(
    f"{outdir}/Report/pairtools/{{library}}.dedup.stats",
    library=SAMPLE_FASTQS.keys(),
)
# Complexity calculations
library_report += expand(
    f"{outdir}/Report/pairtools/{{library}}.complexity.tsv",
    library=SAMPLE_FASTQS.keys(),
)
# Downstream distance vs contact statistics
library_report += expand(
    f"{outdir}/Report/downstream/{{library}}.{{filter_name}}.{min_resolution}_loglog_fits.csv",
    library=SAMPLE_FASTQS.keys(),
    filter_name=list(config["bin"]["filters"].keys()),
)
# Loop counts summary
library_report += expand(
    f"{outdir}/Report/downstream/{{library}}.{{filter_name}}.loop_counts.tsv",
    library=SAMPLE_FASTQS.keys(),
    filter_name=list(config["bin"]["filters"].keys()),
)
library_report += expand(
    f"{outdir}/Report/Pairs/{{library}}.hicpro.RSstat",
    library=SAMPLE_FASTQS.keys(),
)
# Scaling reports (if enabled)
if "scaling_pairs" in config and config["scaling_pairs"].get("do", True):
    library_report += expand(
        f"{outdir}/Report/Pairs/{{library}}.nodups.scaling.tsv",
        library=SAMPLE_FASTQS.keys(),
    )

# MultiQC report
library_report.append(f"{outdir}/Report/multiqc_report.html")

library_hic = []
if config["bin"].get("make_hic", False):
    library_hic += expand(
        f"{outdir}/Important_processed/hic/{{library}}.{{filter_name}}.{min_resolution}.hic",
        library=SAMPLE_FASTQS.keys(),
        filter_name=list(config["bin"]["filters"].keys()),
    )

# Setting up the library group cooler and group stats outputs
library_group_coolers = []
library_group_hic = []
if "library_groups" in config["input"] and len(config["input"]["library_groups"]) > 0:
    library_group_coolers = expand(
        f"{outdir}/Important_processed/Cooler/{{library_group}}.{{filter_name}}.{min_resolution}.mcool",
        library_group=config["input"]["library_groups"].keys(),
        filter_name=list(config["bin"]["filters"].keys()),
    )
    if config["bin"].get("make_hic", False):
        library_group_hic = expand(
            f"{outdir}/Important_processed/hic/{{library_group}}.{{filter_name}}.{min_resolution}.hic",
            library_group=config["input"]["library_groups"].keys(),
            filter_name=list(config["bin"]["filters"].keys()),
        )
multiqc = [f"{outdir}/Report/multiqc_report.html"]
allvalidPair = []
for sample in SAMPLE_FASTQS.keys():
    allvalidPair.append(f"{outdir}/Important_processed/Pairs/{sample}.hicpro.validPairs")

# Rule order for mapper-specific dependencies
if config["map"]["mapper"] == "chromap":
    ruleorder: map_chunks_chromap > parse_sort_chunks
elif config["map"]["mapper"] == "bowtie2":
    pass
else:
    ruleorder: parse_sort_chunks > map_chunks_chromap

# 4. Target Rule
rule all:
    input:
        # QC & Stats
        multiqc,
        # Core Outputs
        library_coolers,
        library_group_coolers if "library_groups" in config["input"] and len(config["input"]["library_groups"]) > 0 else [],
        library_group_hic if "library_groups" in config["input"] and len(config["input"]["library_groups"]) > 0 and config["bin"].get("make_hic", False) else [],
        
        # Conditional Outputs
        library_hic if config["bin"].get("make_hic", False) else [],
        library_scaling_pairs if config.get("scaling_pairs", {}).get("do", True) else [],
        library_report,
        
        # Downstream
        library_mustache_loops,
        library_downstream_stat,
        allvalidPair
